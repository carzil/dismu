buildscript {
    repositories { mavenCentral() }
    dependencies { classpath 'com.android.tools.build:gradle:1.0.0' }
}

allprojects {
    apply plugin: 'idea'
    group = 'com.dismu'
    version = '1.0-SNAPSHOT'

    repositories {
        mavenCentral()
        mavenLocal()
    }
}

project(':commons') {
    apply plugin: 'java'

    dependencies {
        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.5'
        compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.5'
        compile group: 'log4j', name: 'log4j', version: '1.2.17'
        compile (group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1') {
            exclude group: 'org.hamcrest'
        }
        compile group: 'org', name: 'jaudiotagger', version: '2.0.3'
        compile group: 'de.u-mass', name: 'lastfm-java', version: '0.1.2'
        compile group: 'net.jpountz.lz4', name: 'lz4', version: '1.3.0'

    }
}

project(':client') {
    apply plugin: 'java'
    apply plugin: 'application'

    dependencies {
        compile project(':commons')
        compile group: 'de.huxhorn.sulky', name: 'de.huxhorn.sulky.3rdparty.jlayer', version: '1.0'
        compile group: 'com.googlecode.soundlibs', name: 'mp3spi', version: '1.9.5-1'
        compile group: 'com.mpatric', name: 'mp3agic', version: '0.8.2'
        compile group: 'com.intellij', name: 'forms_rt', version: '7.0.3'
        compile files('./lib/jflac-1.3.jar')
    }
}

project(':android') {
    apply plugin: 'android'

    dependencies {
        compile project(':commons')
        compile group: 'de.mindpipe.android', name: 'android-logging-log4j', version: '1.0.2'
    }
}
